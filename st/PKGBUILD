# Maintainer: William Turner <willtur.will@gmail.com>

_pkgname=st
pkgname="${_pkgname}-willtrnr"
pkgver=0.9
pkgrel=1
pkgdesc="A simple virtual terminal emulator for X."
arch=('i686' 'x86_64' 'armv7h' 'aarch64')
url='https://st.suckless.org'
license=('MIT')
depends=('harfbuzz'
         'libxft')
provides=('st')
conflicts=('st')
source=("https://dl.suckless.org/${_pkgname}/${_pkgname}-${pkgver}.tar.gz"
        "st-desktopentry.patch::https://st.suckless.org/patches/desktopentry/st-desktopentry-0.8.5.diff"
        "st-scrollback.patch::https://st.suckless.org/patches/scrollback/st-scrollback-ringbuffer-0.8.5.diff"
        "st-scrollback-mouse.patch::https://st.suckless.org/patches/scrollback/st-scrollback-mouse-20220127-2c5edf2.diff"
        "st-ligatures.patch::https://st.suckless.org/patches/ligatures/0.9/st-ligatures-scrollback-ringbuffer-20230105-0.9.diff"
        "st-nordtheme.patch::https://st.suckless.org/patches/nordtheme/st-nordtheme-0.8.5.diff"
        terminfo.patch
        font.patch)
sha256sums=('f36359799734eae785becb374063f0be833cf22f88b4f169cd251b99324e08e7'
            '9c59fdeaab64e3cc2db524ab6c12147476a9792c46a217e3a5f47029a42919da'
            'b054f3c4b5a076645aecd927658ae7027a0c4b859cbd0b9a42fab07fdf7cbc77'
            '46ac9bcdbfeb0011533207cb0ab31657a3eb9196da1d0db346e6a9d1fc4b4f76'
            'd2412082c17bcbea892ccbd9c295f2287240e7033c5002c0da7445133981a402'
            '4e5a69d470f8be5fdcf3c531235b78ad2ee70c4ccc9037b25bbfe8a48bf845ff'
            '8b0c115f89011fd2a2d0ca398aaafc482e67331bbb72d2731f1684d9b5c6b5a3'
            'eb7599b23d8046c42046220e8511be85d8a6358cba0e9fe51ad2427b77d5b03d')

prepare() {
  cd "${_pkgname}-${pkgver}"

  for f in "${source[@]}"; do
    if [[ "${f%%::*}" == *.patch ]]; then
      printf 'Patch %s\n' "${f%%::*}"
      patch -p1 -i "${srcdir}/${f%%::*}"
    fi
  done
}

build() {
  cd "${_pkgname}-${pkgver}"

  make X11INC=/usr/include/X11 X11LIB=/usr/lib/X11
}

package() {
  cd "${_pkgname}-${pkgver}"

  make PREFIX=/usr DESTDIR="${pkgdir}" install

  install -Dm 0644 st.info "${pkgdir}/usr/share/doc/${pkgname}/st.info"
  install -Dm 0644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

# Maintainer: William Turner <willtur.will@gmail.com>

_pkgname=st
pkgname="${_pkgname}-willtrnr"
pkgver=0.9.2
pkgrel=1
pkgdesc="A simple virtual terminal emulator for X."
arch=('i686' 'x86_64' 'armv7h' 'aarch64')
url='https://st.suckless.org'
license=('MIT')
depends=('fontconfig'
         'freetype2'
         'harfbuzz'
         'libx11'
         'libxft')
provides=('st')
conflicts=('st')
source=("https://dl.suckless.org/${_pkgname}/${_pkgname}-${pkgver}.tar.gz"
        "st-desktopentry.patch::https://st.suckless.org/patches/desktopentry/st-desktopentry-0.8.5.diff"
        "st-delkey.patch::https://st.suckless.org/patches/delkey/st-delkey-20201112-4ef0cbd.diff"
        "st-scrollback.patch::https://st.suckless.org/patches/scrollback/st-scrollback-20210507-4536f46.diff"
        "st-scrollback-mouse.patch::https://st.suckless.org/patches/scrollback/st-scrollback-mouse-20220127-2c5edf2.diff"
        "st-hidecursor.patch::https://st.suckless.org/patches/hidecursor/st-hidecursor-0.8.3.diff"
        "st-ligatures.patch::https://st.suckless.org/patches/ligatures/0.9/st-ligatures-scrollback-20240105-0.9.diff"
        "st-nordtheme.patch::https://st.suckless.org/patches/nordtheme/st-nordtheme-0.8.5.diff"
        {font,fork,scrollback,terminfo}.patch)
sha256sums=('6b215d4f472b21d6232f30f221117a777e24bcfee68955ddefb7426467f9494b'
            '9c59fdeaab64e3cc2db524ab6c12147476a9792c46a217e3a5f47029a42919da'
            '946051d123dfe21d8f5ef0f1070c473443f4779dc0bd7edf7c8497f67e325a49'
            '19d8f4e7fd0d1933dc6fcf6c7333db08e1b40fc75795464660c4d723eb62511c'
            '46ac9bcdbfeb0011533207cb0ab31657a3eb9196da1d0db346e6a9d1fc4b4f76'
            '31bb2d8f2a297ec8854d990cc923f813604e3cbc3265432e0078b73b2e5614dd'
            '591fa8fd19bc60f26809d9ad5e7e5c4e1f878da0c93abcc9220df6b1704e98da'
            '4e5a69d470f8be5fdcf3c531235b78ad2ee70c4ccc9037b25bbfe8a48bf845ff'
            'eb7599b23d8046c42046220e8511be85d8a6358cba0e9fe51ad2427b77d5b03d'
            '530ed2fea90670c402b740070e652bd8531d4be5bd20178b36edaf70d3b6fc3d'
            '71f0569584e3698d0cc6503d2143738da1bac8586d285c844f7334795c2f6749'
            '8b0c115f89011fd2a2d0ca398aaafc482e67331bbb72d2731f1684d9b5c6b5a3')

prepare() {
  cd "${_pkgname}-${pkgver}"

  # Adjust for -p1
  sed -i 's;config.def.h;a/config.def.h;' "${srcdir}/st-delkey.patch"

  for f in "${source[@]}"; do
    if [[ "${f%%::*}" == *.patch ]]; then
      printf 'Patch %s\n' "${f%%::*}"
      patch -p1 -N -i "${srcdir}/${f%%::*}" --merge
    fi
  done
}

build() {
  cd "${_pkgname}-${pkgver}"

  make X11INC=/usr/include/X11 X11LIB=/usr/lib/X11
}

package() {
  cd "${_pkgname}-${pkgver}"

  make PREFIX=/usr DESTDIR="${pkgdir}" install

  install -Dm 0644 st.info "${pkgdir}/usr/share/doc/${pkgname}/st.info"
  install -Dm 0644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

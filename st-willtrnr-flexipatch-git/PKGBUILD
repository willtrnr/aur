# Maintainer: William Turner <willtur.will@gmail.com>

_pkgname=st
pkgname="${_pkgname}-willtrnr-flexipatch-git"
pkgver=0.9.2.aa59574
pkgrel=2
pkgdesc="A simple virtual terminal emulator for X."
arch=('i686' 'x86_64' 'armv7h' 'aarch64')
url='https://st.suckless.org'
license=('MIT')
depends=('fontconfig'
         'freetype2'
         'harfbuzz'
         'imlib2'
         'libx11'
         'libxft')
provides=('st')
conflicts=('st')
replaces=('st-willtrnr')
source=("${_pkgname}::git+https://github.com/bakkeby/st-flexipatch.git"
        terminfo.patch
        fork.patch
        font.patch
        nordtheme.patch
        scrollback.patch)
sha256sums=('SKIP'
            'b081c1f887fef920aa8ee16c838a02f08ad0ca8fb13c68b4bd0cbc58c3af8921'
            '468e55037f10e9327e5a2a22f9fafae3fc2ad9a97354eb5be10710a44f2b1fdc'
            '92b6bc30cc0c46258f718c4af5981759e883fa1bcd780c4d73e2ebfb55aaf023'
            '6f9098cce962ea3bd7c481d2d0812877afc5719bd51b1d5e21194d4f4560dcff'
            '80c3956efec6fd2002a9cfb5a4c59877d81a92b2e093d4433836667334e5b22e')

pkgver() {
  cd "${_pkgname}"

  printf '%s.%s' "$(sed -n 's/^VERSION = \(.*\)$/\1/p' config.mk)" "$(git rev-parse --short=7 HEAD)"
}

prepare() {
  cd "${_pkgname}"

  for f in "${source[@]}"; do
    if [[ "${f%%::*}" == *.patch ]]; then
      printf 'Patch %s\n' "${f%%::*}"
      patch -p1 -N -i "${srcdir}/${f%%::*}" --merge
    fi
  done

  for p in BOXDRAW CSI_22_23 DELKEY LIGATURES NEWTERM REFLOW SCROLLBACK SCROLLBACK_MOUSE SIXEL W3M; do
    sed -i 's/^#define '"$p"'_PATCH.*$/#define '"$p"'_PATCH 1/' patches.def.h
    sed -i 's/^#'"$p"'_/'"$p"'_/' config.mk
  done

}

build() {
  cd "${_pkgname}"

  make X11INC=/usr/include/X11 X11LIB=/usr/lib/X11
}

package() {
  cd "${_pkgname}"

  make PREFIX=/usr DESTDIR="${pkgdir}" install

  install -Dm 0644 st.info "${pkgdir}/usr/share/doc/${pkgname}/st.info"
  install -Dm 0644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

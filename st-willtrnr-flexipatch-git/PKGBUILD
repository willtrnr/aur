# Maintainer: William Turner <willtur.will@gmail.com>

_pkgname=st
pkgname="${_pkgname}-willtrnr-flexipatch-git"
pkgver=0.9.2.8a024a1
pkgrel=1
pkgdesc="A simple virtual terminal emulator for X."
arch=('i686' 'x86_64' 'armv7h' 'aarch64')
url='https://st.suckless.org'
license=('MIT')
depends=('fontconfig'
         'freetype2'
         'harfbuzz'
         'imlib2'
         'libx11'
         'libxft')
provides=('st')
conflicts=('st')
replaces=('st-willtrnr')
source=("${_pkgname}::git+https://github.com/bakkeby/st-flexipatch.git"
        boxdraw.patch
        font.patch
        fork.patch
        nordtheme.patch
        scrollback.patch
        terminfo.patch)
sha256sums=('SKIP'
            '9057ac21f2fb24c349004862ee40f9e9101bdc721f96aa75ec15564e089a30db'
            'd94d06ded5ee18be750d0159458c71e71c9e199391213e6a670dab4a0c4bf0d3'
            '468e55037f10e9327e5a2a22f9fafae3fc2ad9a97354eb5be10710a44f2b1fdc'
            'dcb86da8e6f8e4341b04454c6c92f22dfd30b7f7cb195ffab9598964cb571daa'
            'aa3c245ea849341dea410964f409d1cff91a03366cb3e1dadb51a642d433e8ac'
            'b081c1f887fef920aa8ee16c838a02f08ad0ca8fb13c68b4bd0cbc58c3af8921')

_patches=(
  BOXDRAW
  CSI_22_23
  DELKEY
  FIXKEYBOARDINPUT
  HIDE_TERMINAL_CURSOR
  LIGATURES
  NEWTERM
  REFLOW
  SCROLLBACK
  SCROLLBACK_MOUSE
  SIXEL
  UNDERCURL
  W3M
  WIDE_GLYPHS
)

pkgver() {
  cd "${_pkgname}"

  printf '%s.%s' "$(sed -n 's/^VERSION = \(.*\)$/\1/p' config.mk)" "$(git rev-parse --short=7 HEAD)"
}

prepare() {
  cd "${_pkgname}"

  cp config.def.h config.h
  cp patches.def.h patches.h

  for f in "${source[@]}"; do
    f="${f%%::*}"
    if [[ "$f" == *.patch ]]; then
      printf 'Patch %s\n' "$f"
      patch -p1 -N -i "${srcdir}/$f" --merge
    fi
  done

  for p in "${_patches[@]}"; do
    sed -i 's/^#define '"$p"'_PATCH.*$/#define '"$p"'_PATCH 1/' patches.h
    sed -i 's/^#'"$p"'_/'"$p"'_/' config.mk
  done

}

build() {
  cd "${_pkgname}"

  make X11INC=/usr/include/X11 X11LIB=/usr/lib/X11
}

package() {
  cd "${_pkgname}"

  make PREFIX=/usr DESTDIR="${pkgdir}" install

  install -Dm 0644 st.info "${pkgdir}/usr/share/doc/${pkgname}/st.info"
  install -Dm 0644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

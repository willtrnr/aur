# Maintainer: William Turner <willtur.will@gmail.com>

_pkgbase=ttf-berkeley-mono
_pkgvariant=-slash-lig
_pkgname="${_pkgbase}-nerd"
_fontver=2.004
_nerdver=3.4.0
pkgname="${_pkgname}${_pkgvariant}"
pkgver="${_fontver}+${_nerdver}"
pkgrel=1
pkgdesc='A monospace font for professionals. (nerd patched) (slashed 0 with ligatures)'
arch=('any')
url='https://usgraphics.com/products/berkeley-mono'
license=('custom')
makedepends=('fontforge'
             "${_pkgbase}${_pkgvariant}=${_fontver}")
provides=("${_pkgname}")
conflicts=("${_pkgname}")
source=("https://github.com/ryanoasis/nerd-fonts/releases/download/v${_nerdver}/FontPatcher.zip"
        "31-${_pkgname}.conf"
        "41-${_pkgname}.conf"
        "61-${_pkgname}.conf")
sha256sums=('a8f11e511ed7c69e96680858c06b50a643ea7752e26d5cd13dd5e5cc53ab1760'
            '1b2e94c17cf4d0e8f562502a71c2b96c6fb0cf40a881b22e4cf5c54262d7aa1f'
            '2bcf76bc879eede708e8bd7cf8a8f516937a3c803d037efc71bdbe66a8eba48b'
            'e28c39c2587a767a90c7fee890ba76cd027290aa5b8678e1428721602f4ce829')

_styles=(
  Thin{,-Oblique}
  Light{,-Oblique}
  Regular Oblique
  Medium{,-Oblique}
  Bold{,-Oblique}
  Black{,-Oblique}
)

build() {
  for s in "${_styles[@]}"; do
    fontforge \
      -quiet \
      -script font-patcher \
      --mono \
      --debug 1 \
      --quiet \
      --careful \
      --outputdir build \
      --makegroups 2 \
      --complete \
      --has-no-italic \
      --no-progressbars \
      "/usr/share/fonts/TTF/TX-02-${s//-/}.ttf"
  done
}

package() {
  for s in "${_styles[@]}"; do
    install -D -m0644 "build/TX02NerdFontMono-${s//-/}.ttf" -t "${pkgdir}/usr/share/fonts/TTF/"
  done

  install -D -m0644 "/usr/share/licenses/${_pkgbase}${_pkgvariant}/README.txt" -t "${pkgdir}/usr/share/licenses/${pkgname}/"

  install -D -m0644 ./*.conf -t "${pkgdir}/usr/share/fontconfig/conf.avail/"
  install -d -m0755 "${pkgdir}/usr/share/fontconfig/conf.default/"
  ln -srt "${pkgdir}/usr/share/fontconfig/conf.default/" "${pkgdir}/usr/share/fontconfig/conf.avail/"*.conf
}

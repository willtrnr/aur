# Maintainer: William Turner <willtur.will@gmail.com>

pkgname=wslutils
pkgver=0.1.0
pkgrel=1
pkgdesc='Collection of utilities and systemd units for WSL'
arch=('any')
url='https://github.com/willtrnr/aur'
license=('MIT')
source=(wsl-{distro-name,disk}
        wsl-{attach-vhd@,root-shared}.service)
sha256sums=('6539bc1b6fafd77d5f029186da784dd2678c07d0cf55fe44ce3e6ccb1a10ae2b'
            '5ed55314e9dd608c713304e5d61a996611efa05c7d2ee2d2562d0f9edbc5fe69'
            'a16d9116b927fead5cd8d6374be200437cc8724ef784e3844bc027da4d5880fd'
            '9e387a58800315ebf44dffe3f800f0ea61ef372e83996debb2ad3dc33933b126')

package() {
  for f in "${source[@]}"; do
    case "$f" in
      *.service)
        install -D -m0644 "$f" "${pkgdir}/usr/lib/systemd/system/$f"
        ;;
      *)
        install -D -m0755 "$f" "${pkgdir}/usr/bin/$f"
        ;;
    esac
  done
}

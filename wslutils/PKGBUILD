# Maintainer: William Turner <willtur.will@gmail.com>

pkgname=wslutils
pkgver=0.1.1
pkgrel=1
pkgdesc='Collection of utilities and services for WSL'
arch=('any')
url='https://github.com/willtrnr/aur'
license=('MIT')
optdepends=('socat: X11 forwarding'
            'resolvconf: Resolvconf support')
source=(wsl-{distro-name,disk}
        wsl-attach-vhd@.service
        wsl-resolvconf@.service
        wsl-root-shared.service
        wsl-x11-vsock@.{service,socket})
sha256sums=('6539bc1b6fafd77d5f029186da784dd2678c07d0cf55fe44ce3e6ccb1a10ae2b'
            '5ed55314e9dd608c713304e5d61a996611efa05c7d2ee2d2562d0f9edbc5fe69'
            'a16d9116b927fead5cd8d6374be200437cc8724ef784e3844bc027da4d5880fd'
            '387983069a7edb86e18bfcc5aaf339c725ab2949d913f9713369c8dfc49c3747'
            '9e387a58800315ebf44dffe3f800f0ea61ef372e83996debb2ad3dc33933b126'
            'cc87e2d7b16d30239b1382cea9b69e996ffea678bf8c470d18acb2e10c96a7bb'
            'c5d6a7dfed7f6e9fb441ce7370e8bd8d9d701b583029dcc1b3297af87b532e5a')

package() {
  for f in "${source[@]}"; do
    case "$f" in
      *.service|*.socket)
        install -D -m0644 "$f" "${pkgdir}/usr/lib/systemd/system/$f"
        ;;
      *)
        install -D -m0755 "$f" "${pkgdir}/usr/bin/$f"
        ;;
    esac
  done
}

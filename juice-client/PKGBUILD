# Maintainer: William Turner <willtur.will@gmail.com>
pkgname=juice-client
_pkgname=JuiceClient
pkgver=2023.06.07.136
_pkgver=2023.06.07-0136.d9bc8ad1
pkgrel=1
epoch=
pkgdesc="Juice GPU-over-IP - Client"
arch=(x86_64)
url="https://www.juicelabs.co/"
license=('custom')
depends=(ffmpeg4.4
         glibc
         nvidia-utils
         vulkan-icd-loader)
backup=("opt/${pkgname}/juice.cfg")
source=("https://github.com/Juice-Labs/Juice-Labs/releases/download/${_pkgver}/${_pkgname}-linux.tar.gz"
        tmpfile.conf)
sha256sums=('4e13270bef69c58f8c9f71a817f82412d47843bb62fc24fb3e32a327f55148dc'
            '55858c6f1dca1aa6d0d3ba06daa6599cc63b7798cc6f59810567d8fc955ce73b')

prepare() {
  sed -i "s/@@PKGNAME@@/${pkgname}/" tmpfile.conf
}

package() {
  install -d -m755 "${pkgdir}/opt/${pkgname}"

  install -m755 juicify "${pkgdir}/opt/${pkgname}/"

  for f in *.so*; do
    install -m644 "$f" "${pkgdir}/opt/${pkgname}/"
  done

  for f in dxvk.conf juice.cfg version.txt; do
    install -m644 "$f" "${pkgdir}/opt/${pkgname}/"
  done

  install -d -m755 "${pkgdir}/usr/bin"
  ln -s "/opt/${pkgname}/juicify" "${pkgdir}/usr/bin/juicify"

  install -D -m644 tmpfile.conf "${pkgdir}/usr/lib/tmpfiles.d/${pkgname}.conf"

  install -D -m644 TERMS.md "${pkgdir}/usr/share/licenses/${pkgname}/TERMS.md"
}

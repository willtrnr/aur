# Maintainer: William Turner <willtur.will@gmail.com>

pkgname=ravi
_pkgver=1.0-beta11
pkgver="${_pkgver//-/.}"
pkgrel=1
pkgdesc="Dialect of Lua, featuring limited optional static typing, JIT and AOT compilers."
arch=(x86_64)
url="http://ravilang.github.io/"
license=('MIT')
depends=(glibc)
makedepends=(cmake git)
source=("$pkgname-$pkgver::git+https://github.com/dibyendumajumdar/ravi.git#tag=$_pkgver")
sha256sums=('SKIP')

prepare() {
	cd "$pkgname-$pkgver"

  sed -i 's;usr/local;usr;' include/luaconf.h
  sed -i "s;DESTINATION cmake;DESTINATION lib/cmake/$pkgname;" CMakeLists.txt
}

build() {
	cd "$pkgname-$pkgver"

  cmake -B build \
    -D CMAKE_BUILD_TYPE=RELEASE \
    -D CMAKE_INSTALL_PREFIX=/usr

  make -C build
}

package() {
	cd "$pkgname-$pkgver"

  make -C build install DESTDIR="$pkgdir"
}

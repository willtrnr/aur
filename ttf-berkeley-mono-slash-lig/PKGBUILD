# Maintainer: William Turner <willtur.will@gmail.com>

_pkgbase=ttf-berkeley-mono
_pkgvariant=-slash-lig
_pkgname="${_pkgbase}"
pkgname="${_pkgname}${_pkgvariant}"
pkgver=2.0.0
pkgrel=1
pkgdesc='A monospace font for professionals. (slashed 0 with ligatures)'
arch=('any')
url='https://usgraphics.com/products/berkeley-mono'
license=('custom')
provides=("${_pkgname}")
conflicts=("${_pkgname}")
_package=2412274379MM5M84
_ticket=4ZRWWJWP
source=("${_package}.zip"
        "32-${_pkgname}.conf"
        "42-${_pkgname}.conf"
        "62-${_pkgname}.conf")
sha256sums=('1c4a92d75d9b139feacf4e8896dbe88aba1b6c1381c5a9e50541a681a7eddaac'
            '00546c55b18be6da335c63415a3c0f23f0c8780d29972d48f52ee8b921767b4b'
            'aa91fcc1a30aad67ac017105f936ef0e3069249f8740775fed81673a706ebdb9'
            '1c3d8b0fdda39a1a6eb24a508a7822bd12b27dae2ca3e912d8606d54f7167ba5')

_styles=(Regular Bold Oblique Bold-Oblique)

package() {
  for s in "${_styles[@]}"; do
    install -D -m0644 "${_package}/TX-02-${_ticket}/TX-02-${s}.ttf" "${pkgdir}/usr/share/fonts/TTF/TX-02-${s//-/}.ttf"
  done

  install -D -m0644 "${_package}/README.txt" -t "${pkgdir}/usr/share/licenses/${pkgname}/"

  install -D -m0644 ./*.conf -t "${pkgdir}/usr/share/fontconfig/conf.avail/"
  install -d -m0755 "${pkgdir}/usr/share/fontconfig/conf.default/"
  ln -srt "${pkgdir}/usr/share/fontconfig/conf.default/" "${pkgdir}/usr/share/fontconfig/conf.avail/"*.conf
}

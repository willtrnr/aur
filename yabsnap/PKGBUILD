# Maintainer: William Turner <willtur.will@gmail.com>

pkgname=yabsnap
pkgver=2.1.4
pkgrel=1
pkgdesc='Btrfs automated snapshot manager.'
arch=('any')
url='https://github.com/hirak99/yabsnap'
license=('Apache')
depends=('bash'
         'btrfs-progs'
         'python>=3.12' 'python<3.13')
makedepends=('rsync')
source=("${pkgname}-${pkgver}.tar.gz::${url}/archive/refs/tags/v${pkgver}.tar.gz")
sha256sums=('8752b66ffa511028be028c3804d373ad17bf4eb7db1c101ed5112402cfe4ac90')

prepare() {
  cd "${pkgname}-${pkgver}"

  rm -f src/code/.gitignore
}

build() {
  cd "${pkgname}-${pkgver}"

  gzip -fk artifacts/yabsnap.manpage
}

package() {
  cd "${pkgname}-${pkgver}"

  # Install program code
  install -Dm 0755 src/yabsnap.sh "${pkgdir}/usr/share/${pkgname}/${pkgname}.sh"
  cp -r src/code "${pkgdir}/usr/share/${pkgname}/code"
  rm -f "${pkgdir}/usr/share/${pkgname}/code/"*_test.py

  # Install path link
  install -dm 0755 "${pkgdir}/usr/bin"
  ln -sf "../share/${pkgname}/${pkgname}.sh" "${pkgdir}/usr/bin/${pkgname}"

  # Pre-compile all python files
  python3 -m compileall -f -s "${pkgdir}" -e "${pkgdir}" "${pkgdir}/usr/share/${pkgname}/code"

  # Add pacman hook
  install -Dm 0644 artifacts/pacman/01-yabsnap-pacman-pre.hook "${pkgdir}/usr/share/libalpm/hooks/01-${pkgname}-pacman-pre.hook"

  # Add systemd units
  install -Dm 0644 artifacts/services/yabsnap.service "${pkgdir}/usr/lib/systemd/system/${pkgname}.service"
  install -Dm 0644 artifacts/services/yabsnap.timer "${pkgdir}/usr/lib/systemd/system/${pkgname}.timer"

  # Install man page
  install -Dm 0644 artifacts/yabsnap.manpage.gz "${pkgdir}/usr/share/man/man1/${pkgname}.1.gz"

  # Add package license for compliance
  install -Dm 0644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"

  # Track the config path
  install -dm 0755 "${pkgdir}/etc/yabsnap/configs"
}

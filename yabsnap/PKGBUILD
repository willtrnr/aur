# Maintainer: Arnab Bose <hirak99+arch@gmail.com>

pkgname=yabsnap
pkgver=2.0.2
pkgrel=1
pkgdesc="Btrfs automated snapshot manager."
arch=('any')
url="https://github.com/hirak99/yabsnap"
license=('Apache')
# Dependencies -
# btrfs-progs: Required for btrfs operations. May change if we extend into rsync
#   rsync based options.
# coreutils: For stat. Should be part of base.
#   Opinion is split on whether to include coreutils, err on including it.
# python3: Runtime. Typically present in a base install.
depends=('btrfs-progs' 'coreutils' 'python3')
makedepends=('rsync')
provides=('yabsnap')
source=("$url/archive/refs/tags/v$pkgver.tar.gz"
        01-fix-install-script.patch)
sha256sums=('dbf1f2a9be6174e36e74e5cff96215691ee7212243c57e5b30315d7b66bcc7bd'
            '390fedf1a0690618e69a0a714fc602e00e6b6deae556dbafffc1bcb456a9dfaa')

prepare() {
  cd "$pkgname"-$pkgver
  patch -p1 -i "$srcdir/01-fix-install-script.patch"
}

check() {
  cd "$pkgname"-$pkgver
  src/run_tests.sh
}

package() {
  cd "$pkgname"-$pkgver
  scripts/install-to-dest.sh "$pkgdir"
}

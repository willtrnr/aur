# Maintainer: CÃ©dric Connes <cedric.connes@gmail.com>

pkgname=mega-sdk
pkgver=3.5.2
pkgrel=1
pkgdesc="Official mega.nz SDK"
arch=('i686' 'x86_64')
url="https://github.com/meganz/sdk"
license=('BSD-2-Clause')
depends=('c-ares'
         'crypto++'
         'curl'
         'fuse'
         'libsodium'
         'libuv'
         'openssl'
         'readline'
         'sqlite'
         'zlib')
makedepends=(swig)
source=("https://github.com/meganz/sdk/archive/v$pkgver.tar.gz")
sha256sums=('0d2c0ce2964d3fcd234631f18c01eb2b53b6aece3079220d2a2b4ba535761ff5')

build() {
  cd "sdk-$pkgver"
  ./autogen.sh
  ./configure \
		--prefix=/usr \
		--without-freeimage \
		--disable-silent-rules \
		--disable-examples \
		--enable-python \
		--with-python3
  make
}

package() {
  cd "sdk-$pkgver"
  make DESTDIR="$pkgdir/" install
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# Maintainer: William Turner <willtur.will@gmail.com>

pkgname=git-credential-netrc
# renovate: datasource=github-tags depName=git/git
pkgver=2.53.0
pkgrel=1
pkgdesc="git-credentials helper for netrc"
arch=('x86_64')
url='https://git-scm.com/'
license=('GPL2')
depends=('perl')
makedepends=('python')
source=("https://www.kernel.org/pub/software/scm/git/git-${pkgver}.tar."{xz,sign})
sha256sums=('3cd8fee86f69a949cb610fee8cd9264e6873d07fa58411f6060b3d62729ed7c5'
            'SKIP')
validpgpkeys=('96E07AF25771955980DAD10020D04E5A713660A7')

_make() {
  local make_options=(
    prefix='/usr'
    gitexecdir='/usr/lib/git-core'
    perllibdir="$(/usr/bin/perl -MConfig -wle 'print $Config{installvendorlib}')"

    CFLAGS="${CFLAGS}"
    LDFLAGS="${LDFLAGS}"
    INSTALL_SYMLINKS=1
    MAN_BOLD_LITERAL=1
    NO_PERL_CPAN_FALLBACKS=1
    USE_LIBPCRE2=1
  )

  make "${make_options[@]}" "$@"
}

build() {
	cd "${srcdir}/git-${pkgver}"

	_make -C contrib/credential/netrc build
}

package() {
	cd "${srcdir}/git-${pkgver}"

  install -d -m0755 "${pkgdir}/usr/lib/git-core"

	_make -C contrib/credential/netrc DESTDIR="${pkgdir}" install
}

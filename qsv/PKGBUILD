# Maintainer: William Turner <willtur.will@gmail.com>

_pkgname=qsv
pkgname=qsv-luau-polars
pkgver=0.117.0
pkgrel=1
pkgdesc="Command line program for querying, indexing, slicing, analyzing, filtering, enriching, transforming, sorting, validating & joining CSV files."
arch=('x86_64')
url="https://github.com/jqnatividad/qsv"
license=('MIT')
depends=()
makedepends=('cargo')
provides=('qsv')
conflicts=('qsv')
source=("$pkgname-$pkgver.tar.gz::$url/archive/refs/tags/$pkgver.tar.gz")
sha256sums=('5f74e4380a1fea1b2fb8ae41c852f3fcde3021780e2352351ab20869913ac578')

prepare() {
	cd "$_pkgname-$pkgver"

  export RUSTUP_TOOLCHAIN=stable
  export CARGO_TARGET_DIR=target

  cargo fetch \
    --locked \
    --target="$CARCH-unknown-linux-gnu"
}

build() {
	cd "$_pkgname-$pkgver"

  export RUSTUP_TOOLCHAIN=stable
  export CARGO_TARGET_DIR=target

  cargo build \
    --frozen \
    --release \
    --bin=$_pkgname \
    --features=feature_capable,polars,luau
}

package() {
	cd "$_pkgname-$pkgver"

  install -Dm0755 target/release/$_pkgname "$pkgdir/usr/bin/$_pkgname"
}

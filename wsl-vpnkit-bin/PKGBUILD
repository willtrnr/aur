# Maintainer: William Turner <willtur.will@gmail.com>

_pkgname=wsl-vpnkit
pkgname="${_pkgname}-bin"
# renovate: datasource=github-releases depName=containers/gvisor-tap-vsock
pkgver=0.8.8
pkgrel=1
pkgdesc='Provides network connectivity to WSL 2 when blocked by VPN'
arch=('x86_64')
url='https://github.com/sakai135/wsl-vpnkit'
license=('Apache-2.0')
depends=('iproute2'
         'iptables')
provides=("${_pkgname}")
conflicts=("${_pkgname}")
source=("https://github.com/containers/gvisor-tap-vsock/releases/download/v${pkgver}/"{gvforwarder,gvproxy-windows.exe}
        "https://github.com/containers/gvisor-tap-vsock/raw/refs/tags/v${pkgver}/LICENSE"
        80-wsltap.network
        wsl-vpnkit.service
        wsl-vpnkit-fw)
sha256sums=('4b674c0f4b8407c909cb440357ace2b8e733783b49b5583541552ef6fdaa592d'
            '08241e3267fde0efdcfd383e77e3c11c8b7099274843b168e3c3824923da591d'
            'cfc7749b96f63bd31c3c42b5c471bf756814053e847c10f3eb003417bc523d30'
            '2dbcf00888c7606c1fcd26fff84db2df5aca64c77f0c61f743951c3fb59b1027'
            '613ef62cb1e9989ab93af7933719749c5aa3fbaa877efa49b64a9c7d7866c46b'
            '4c0a89942bb2c13a09cbede93ed66659207c69591e754bff1c9e749e57efa589')

package() {
  install -D -m0755 gvproxy-windows.exe "${pkgdir}/usr/lib/wsl-vpnkit/wsl-gvproxy.exe"
  install -D -m0755 gvforwarder "${pkgdir}/usr/lib/wsl-vpnkit/wsl-gvforwarder"

  install -D -m0755 wsl-vpnkit-fw "${pkgdir}/usr/lib/wsl-vpnkit/wsl-vpnkit-fw"

  install -D -m0644 80-wsltap.network "${pkgdir}/usr/lib/systemd/network/80-wsltap.network"
  install -D -m0644 wsl-vpnkit.service "${pkgdir}/usr/lib/systemd/system/wsl-vpnkit.service"

  install -D -m0644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

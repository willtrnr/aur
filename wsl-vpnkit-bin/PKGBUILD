# Maintainer: William Turner <willtur.will@gmail.com>

_pkgname=wsl-vpnkit
pkgname="${_pkgname}-bin"
# renovate: datasource=github-releases depName=sakai135/wsl-vpnkit
pkgver=0.4.1
pkgrel=1
pkgdesc='Provides network connectivity to WSL 2 when blocked by VPN'
arch=('x86_64')
url='https://github.com/sakai135/wsl-vpnkit'
license=('MIT')
depends=('bind'
         'iproute2'
         'iptables'
         'wget')
provides=("${_pkgname}")
conflicts=("${_pkgname}")
source=("${_pkgname}-${pkgver}.tar.gz::https://github.com/sakai135/wsl-vpnkit/releases/download/v${pkgver}/${_pkgname}.tar.gz"
        "https://github.com/sakai135/wsl-vpnkit/raw/refs/tags/v${pkgver}/LICENSE"
        'wsl-vpnkit.service')
noextract=("${_pkgname}-${pkgver}.tar.gz")
sha256sums=('509ef76e6fc0d4d945247b08f323de5b34c6c7ce0b57376680eb8ad7e3a74ed5'
            'e17cdceb658ee2bba093e91c292b810ae413581a3be74828514c02fa76bb756a'
            'c8f5c5c8fa856bac4e3e37f54e5a553ba3f6624dbb436b53d46e7cb81d545a06')

prepare() {
  mkdir -p "${_pkgname}-${pkgver}"
  tar -xf "${_pkgname}-${pkgver}.tar.gz" -C "${_pkgname}-${pkgver}" --strip-components=1 app/
}

package() {
  cd "${_pkgname}-${pkgver}"

  install -D -m0755 wsl-vpnkit "${pkgdir}/usr/bin/${_pkgname}"

  install -D -m0755 wsl-gvproxy.exe "${pkgdir}/usr/lib/${_pkgname}/wsl-gvproxy.exe"
  install -D -m0755 wsl-vm "${pkgdir}/usr/lib/${_pkgname}/wsl-vm"

  install -d -m0755 "${pkgdir}/usr/lib/systemd/system"
  sed "s/@@PKGNAME@@/${_pkgname}/g" "${srcdir}/wsl-vpnkit.service" > "${pkgdir}/usr/lib/systemd/system/${_pkgname}.service"

  install -D -m0644 "${srcdir}/LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

# Maintainer: William Turner <willtur.will@gmail.com>

_pkgname=wsl-vpnkit
pkgname="${_pkgname}-bin"
# renovate: datasource=github-releases depName=sakai135/wsl-vpnkit
pkgver=0.4.1
pkgrel=4
pkgdesc='Provides network connectivity to WSL 2 when blocked by VPN'
arch=('x86_64')
url='https://github.com/sakai135/wsl-vpnkit'
license=('MIT')
depends=('iproute2'
         'iptables')
provides=("${_pkgname}")
conflicts=("${_pkgname}")
source=("${_pkgname}-${pkgver}.tar.gz::https://github.com/sakai135/wsl-vpnkit/releases/download/v${pkgver}/${_pkgname}.tar.gz"
        "https://github.com/sakai135/wsl-vpnkit/raw/refs/tags/v${pkgver}/LICENSE"
        80-wsltap.network
        wsl-vpnkit.service
        wsl-vpnkit-fw)
noextract=("${_pkgname}-${pkgver}.tar.gz")
sha256sums=('509ef76e6fc0d4d945247b08f323de5b34c6c7ce0b57376680eb8ad7e3a74ed5'
            'e17cdceb658ee2bba093e91c292b810ae413581a3be74828514c02fa76bb756a'
            '2dbcf00888c7606c1fcd26fff84db2df5aca64c77f0c61f743951c3fb59b1027'
            '855f98b4d23f790463243ec3078743e868a62fd768f09cb22825b1af29faa11a'
            '0c0ead4ca1d6791e890b49a1ebb4b2431444cd44e1a340779685a956e974ad9d')

prepare() {
  mkdir -p "${_pkgname}-${pkgver}"
  tar -xf "${_pkgname}-${pkgver}.tar.gz" -C "${_pkgname}-${pkgver}" --strip-components=1 app/
}

package() {
  install -D -m0755 "${_pkgname}-${pkgver}/wsl-gvproxy.exe" "${pkgdir}/usr/lib/wsl-vpnkit/wsl-gvproxy.exe"
  install -D -m0755 "${_pkgname}-${pkgver}/wsl-vm" "${pkgdir}/usr/lib/wsl-vpnkit/wsl-vm"

  install -D -m0755 wsl-vpnkit-fw "${pkgdir}/usr/lib/wsl-vpnkit/wsl-vpnkit-fw"

  install -D -m0644 80-wsltap.network "${pkgdir}/usr/lib/systemd/network/80-wsltap.network"
  install -D -m0644 wsl-vpnkit.service "${pkgdir}/usr/lib/systemd/system/wsl-vpnkit.service"

  install -D -m0644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

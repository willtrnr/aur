# Maintainer: William Turner <willtur.will@gmail.com>
# Contributor: Joshua Haase <hahj87@gmail.com>
# Contributor: Jonas Weber <contact@jonasw.de>

pkgname=gpp
pkgver=2.28
pkgrel=1
pkgdesc='General-purpose preprocessor with customizable syntax, suitable for a wide range of preprocessing tasks'
arch=('x86_64')
url='http://en.nothingisreal.com/wiki/GPP'
license=('LGPL2.1')
depends=('glibc')
source=("$pkgname::git+https://github.com/logological/gpp.git#tag=$pkgver")
sha256sums=('SKIP')
validpgpkeys=('28F47A15AB82C216D278DEB92B119C3AEFBF4915')

prepare() {
  cd "$pkgname"

  aclocal
  autoheader
  automake --add-missing
  autoconf
}

build() {
  cd "$pkgname"

  ./configure --prefix=/usr --mandir=/usr/share/man
  make
}

package() {
  cd "$pkgname"

  make DESTDIR="$pkgdir/" install
}

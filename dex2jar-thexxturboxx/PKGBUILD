# Maintainer: William Turner <willtur.will@gmail.com>

_pkgname=dex2jar
pkgname=dex2jar-thexxturboxx
_pkgver=2.1
_pkgpatch=57
pkgver=2.1.57
pkgrel=1
pkgdesc="Tools to work with android .dex and java .class files (Nico Mexis fork)"
arch=(any)
url="https://github.com/ThexXTURBOXx/dex2jar"
license=('Apache')
depends=('java-runtime' 'bash')
provides=("${_pkgname}")
conflicts=("${_pkgname}")
source=("https://github.com/ThexXTURBOXx/${_pkgname}/releases/download/v${_pkgpatch}/dex-tools-${_pkgver}-SNAPSHOT.zip")
sha256sums=('67d6f7c10ddf48162fb212bc4df247a4a10fd9212108f19800654c2b3408928e')

pkgver() {
  echo "${_pkgver}.${_pkgpatch}"
}

prepare() {
  cd "$srcdir/dex-tools-${_pkgver}-SNAPSHOT"

 # Remove Java heap parameters from scripts in favor of JAVA_OPTS env
  sed -i -E 's/(\s+-Xm[sx][0-9]+m)+/ $JAVA_OPTS/g' *.sh
}

package() {
  cd "$srcdir/dex-tools-${_pkgver}-SNAPSHOT"

  mkdir -p "$pkgdir/usr/bin/"
  mkdir -p "$pkgdir/opt/dex2jar/lib/"

  install -m755 *.sh "$pkgdir/opt/dex2jar/"
  install -m644 lib/*.jar "$pkgdir/opt/dex2jar/lib/"

  ln -s /opt/dex2jar/d2j-dex2jar.sh "$pkgdir/usr/bin/dex2jar"
}
